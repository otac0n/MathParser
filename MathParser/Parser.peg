@namespace MathParser
@classname Parser
@using System.Linq
@using System.Linq.Expressions
@using System.Numerics
@members
{
    public Parser(Scope? scope = null)
    {
        this.Scope = scope ?? DefaultScope.Instance;
    }

    public Scope Scope { get; }

    private static void CreateVariable(Cursor state, string variable)
    {
        var existing = state[variable] as Expression;
        if (existing == null)
        {
            state[variable] = Expression.Parameter(typeof(Complex), variable);
        }
    }
}

math <Expression>
    = _ value:statement _ EOF { value }

statement <Expression>
    = lambda
    / disjunction
    / conditional

lambda <LambdaExpression>
    = "D(" _ lambda:lambda _ ")" { Scope.Derivative(lambda) }
    / name:identifier _ names:nameTuple _ ("==" / "=" / ":=") _ body:conditional #{ foreach(var p in names) CreateVariable(state, p); } { Expression.Lambda(body, name, names.Select(p => (ParameterExpression)state[p])) }

nameTuple <IList<string>>
    = "(" _ names:identifier<1,,_ "," _> _ ")" { names }

disjunction <Expression> -memoize
    = left:disjunction _ ("||" / "|" / "∨" / "or") _ right:conjunction { Scope.Or(left, right) }
    / conjunction

conjunction <Expression> -memoize
    = left:conjunction _ ("&&" / "&" / "∧" / "and") _ right:conjunctionRest { Scope.And(left, right) }
    / conjunctionRest

conjunctionRest
    = complement
    / equality

complement <Expression>
    =  ("not" / "!" / "¬") _ complement:complement { Scope.Not(complement) }
    / atomic

atomic <Expression>
    = [Tt] "rue" { Expression.Constant(true) }
    / [Ff] "alse" { Expression.Constant(false) }
    / "(" _ disjunction:disjunction _ ")" { disjunction }

equality <Expression>
    = left:additive _ op:equalityOperator _ right:additive { Scope.Compare(left, op, right) }

equalityOperator <ExpressionType>
    = ("==" / "=" / ":=")        { ExpressionType.Equal }
    / ("!=" / "~=" / "<>" / "≠") { ExpressionType.NotEqual }
    / (">=" / "≥")               { ExpressionType.GreaterThanOrEqual }
    / ">"                        { ExpressionType.GreaterThan }
    / ("<=" / "≤")               { ExpressionType.LessThanOrEqual }
    / "<"                        { ExpressionType.LessThan }

conditional <Expression>
    = additive:additive _  (";" / "if") _ condition:disjunction { Scope.Constraint(condition, additive) }
    / additive

additive <Expression> -memoize
    = left:additive _ "+" _ right:multiplicative { Scope.Add(left, right) }
    / left:additive _ "-" _ right:multiplicative { Scope.Subtract(left, right) }
    / multiplicative

multiplicative <Expression> -memoize
    = left:multiplicative _ ("*" / "×" / "·" / ![-+|]) _ right:negation { Scope.Multiply(left, right) }
    / left:multiplicative _ ("/" / "∕" / "÷") _ right:negation { Scope.Divide(left, right) }
    / negation

negation <Expression>
    = "-" _ negation:negation { Scope.Negate(negation) }
    / "+" _ negation:negation { negation }
    / power

negationPrimary <Expression>
    = "-" _ negation:negationPrimary { Scope.Negate(negation) }
    / "+" _ negation:negationPrimary { negation }
    / primary

power <Expression> -memoize
    = left:primary _ "^" _ right:negation { Scope.Pow(left, right) }
    / primary

primary <Expression> -memoize
    = decimal
    / namedConstant
    / "iif(" _ condition:disjunction _ "," _ consequent:conditional _ "," _ alternative:conditional _ ")" { Scope.Conditional(condition, consequent, alternative) }
    / name:identifier "(" _ arguments:conditional<0,,_ "," _> _ ")" { Scope.Function(name, arguments) }
    / variable
    / "√" _ negation:negationPrimary { Scope.Sqrt(negation) }
    / "(" _ conditional:conditional _ ")" { conditional }
    / "|" _ conditional:conditional _ "|" { Scope.Abs(conditional) }
    / "⌈" _ conditional:conditional _ "⌉" { Scope.Ceiling(conditional) }
    / "⌊" _ conditional:conditional _ "⌋" { Scope.Floor(conditional) }

decimal <Expression>
    = value:([0-9]+ ("." [0-9]+)?) { Expression.Constant(double.Parse(value)) }

namedConstant <Expression>
    = "τ" { Expression.Constant(Math.Tau) }
    / "π" { Expression.Constant(Math.PI) }
    / "e" ![_a-z0-9]i { Expression.Constant(Math.E) }
    / "φ" { Expression.Constant((1 + Math.Sqrt(5)) / 2) }
    / "∞" { Expression.Constant(double.PositiveInfinity) }
    / "i" ![_a-z0-9]i { Expression.Constant(Complex.ImaginaryOne) }

variable <Expression>
    = name:identifier #{ CreateVariable(state, name); } { (Expression)state[name] }

identifier
    = !keyword [_a-z]i [_a-z0-9]i*

keyword
    = [Tt] "rue"
    / [Ff] "alse"
    / "and"
    / "or"
    / "not"
    / "if"

_ = [ \t\r\n]*

EOF
    = !.
    / unexpected:. #error{ "Unexpected character '" + unexpected + "'." }
